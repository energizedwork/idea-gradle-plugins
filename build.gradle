buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
        classpath 'gradle.plugin.com.energizedwork.asl2:asl2-gradle-plugin:1.0'
    }
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.energizedwork.idea-base'
apply plugin: 'com.energizedwork.asl2'
apply plugin: 'codenarc'
apply plugin: 'com.gradle.plugin-publish'

apply from: 'gradle/dependencyRules.gradle'
apply from: 'gradle/idea/idea.gradle'

version = '1.4'

repositories {
    mavenCentral()
}

dependencies {
    codenarc('org.codenarc:CodeNarc:1.2.1') {
        exclude module: 'GMetrics'
    }

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'org.xmlunit:xmlunit-core:2.3.0'
}

codenarc.configFile = rootProject.file('gradle/codenarc/rulesets.groovy')

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=128m'
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

gradlePlugin {
    plugins {
        extendedIdeaPlugin {
            id = 'com.energizedwork.extended-idea'
            implementationClass = 'com.energizedwork.gradle.idea.ExtendedIdeaPlugin'
        }
        ideaBasePlugin {
            id = 'com.energizedwork.idea-base'
            implementationClass = 'com.energizedwork.gradle.idea.IdeaBasePlugin'
        }
    }
}

pluginBundle {
    vcsUrl = 'https://github.com/energizedwork/idea-gradle-plugins'

    plugins {
        extendedIdeaPlugin {
            website = 'https://github.com/energizedwork/idea-gradle-plugins#extended-idea-plugin'
            description = 'A plugin that removes the need for explicit xml manipulation when dealing with some aspects of IntelliJ project configuration.'
            id = 'com.energizedwork.extended-idea'
            displayName = 'Idea configuration extensions plugin'
            tags = ['idea', 'IntelliJ']
        }
        ideaBasePlugin {
            website = 'https://github.com/energizedwork/idea-gradle-plugins#idea-base-convention-plugin'
            description = 'A convention plugin for applying base IntelliJ project configuration.'
            id = 'com.energizedwork.idea-base'
            displayName = 'Idea Base convention plugin'
            tags = ['idea', 'IntelliJ']
        }
    }
}