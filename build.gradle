buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
        classpath 'gradle.plugin.com.energizedwork.asl2:asl2-gradle-plugin:1.0'
    }
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.erdi.idea-base'
apply plugin: 'com.energizedwork.asl2'
apply plugin: 'codenarc'
apply plugin: 'com.gradle.plugin-publish'

apply from: 'gradle/dependencyRules.gradle'
apply from: 'gradle/idea/idea.gradle'

version = '2.0'

repositories {
    mavenCentral()
}

dependencies {
    codenarc('org.codenarc:CodeNarc:1.2.1') {
        exclude module: 'GMetrics'
    }

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'org.xmlunit:xmlunit-core:2.3.0'
}

codenarc.configFile = rootProject.file('gradle/codenarc/rulesets.groovy')

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=128m'
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

gradlePlugin {
    plugins {
        extendedIdeaPlugin {
            id = 'com.github.erdi.extended-idea'
            implementationClass = 'com.github.erdi.gradle.idea.ExtendedIdeaPlugin'
        }
        ideaBasePlugin {
            id = 'com.github.erdi.idea-base'
            implementationClass = 'com.github.erdi.gradle.idea.IdeaBasePlugin'
        }
    }
}

pluginBundle {
    vcsUrl = 'https://github.com/erdi/idea-gradle-plugins'
    website = 'https://github.com/erdi/idea-gradle-plugins#idea-gradle-plugins'

    plugins {
        extendedIdeaPlugin {
            website = 'https://github.com/erdi/idea-gradle-plugins#idea-gradle-plugins'
            description = 'A plugin that removes the need for explicit xml manipulation when dealing with some aspects of IntelliJ project configuration.'
            id = 'com.github.erdi.extended-idea'
            displayName = 'Idea configuration extensions plugin'
            tags = ['idea', 'IntelliJ']
        }
        ideaBasePlugin {
            website = 'https://github.com/erdi/idea-gradle-plugins#idea-gradle-plugins'
            description = 'A convention plugin for applying base IntelliJ project configuration.'
            id = 'com.github.erdi.idea-base'
            displayName = 'Idea Base convention plugin'
            tags = ['idea', 'IntelliJ']
        }
    }
}