buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.6'
        classpath 'gradle.plugin.com.energizedwork.asl2:asl2-gradle-plugin:1.0'
    }
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.energizedwork.idea-junit'
apply plugin: 'com.energizedwork.idea-base'
apply plugin: 'com.energizedwork.asl2'
apply plugin: 'codenarc'
apply plugin: 'com.gradle.plugin-publish'

apply from: 'gradle/dependencyRules.gradle'
apply from: 'gradle/idea/idea.gradle'

version = '1.2'

repositories {
    mavenCentral()
}

dependencies {
    codenarc('org.codenarc:CodeNarc:0.26.0') {
        exclude module: 'GMetrics'
    }

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
    testCompile 'org.xmlunit:xmlunit-core:2.3.0'
}

codenarc.configFile = rootProject.file('gradle/codenarc/rulesets.groovy')

gradlePlugin {
    plugins {
        ideaJunitPlugin {
            id = 'com.energizedwork.idea-junit'
            implementationClass = 'com.energizedwork.gradle.idea.IdeaJunitPlugin'
        }
        ideaProjectComponentsPlugin {
            id = 'com.energizedwork.idea-project-components'
            implementationClass = 'com.energizedwork.gradle.idea.IdeaProjectComponentsPlugin'
        }
        ideaBasePlugin {
            id = 'com.energizedwork.idea-base'
            implementationClass = 'com.energizedwork.gradle.idea.IdeaBasePlugin'
        }
    }
}

pluginBundle {
    vcsUrl = 'https://github.com/energizedwork/idea-gradle-plugins'

    plugins {
        ideaJunitPlugin {
            website = 'https://github.com/energizedwork/idea-gradle-plugins#idea-junit-plugin'
            description = 'A plugin that allows to control settings of default JUnit run configuration in IntelliJ.'
            id = 'com.energizedwork.idea-junit'
            displayName = 'Idea JUnit plugin'
            tags = ['idea', 'IntelliJ', 'JUnit']
        }
        ideaProjectComponentsPlugin {
            website = 'https://github.com/energizedwork/idea-gradle-plugins#idea-project-components-plugin'
            description = 'A plugin that simplifies adding component configurations to IntelliJ project configuration.'
            id = 'com.energizedwork.idea-project-components'
            displayName = 'Idea Project Components plugin'
            tags = ['idea', 'IntelliJ', 'xml']
        }
        ideaBasePlugin {
            website = 'https://github.com/energizedwork/idea-gradle-plugins#idea-base-convention-plugin'
            description = 'A convention plugin for applying base IntelliJ project configuration.'
            id = 'com.energizedwork.idea-base'
            displayName = 'Idea Base convention plugin'
            tags = ['idea', 'IntelliJ']
        }
    }
}